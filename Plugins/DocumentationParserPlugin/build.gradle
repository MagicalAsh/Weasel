plugins {
    id 'java'
    id 'antlr'
}

group = 'us.magicalash.weasel'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
targetCompatibility = '11'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    compileOnly project(':Plugins:PluginApi')

    compileOnly 'com.google.code.gson:gson:2.8.5'
    compileOnly 'org.slf4j:slf4j-api:1.7.25'

    antlr "org.antlr:antlr4:4.7.+" // use ANTLR version 4
    compile "org.antlr:antlr4-runtime:4.7.+" // use ANTLR version 4

    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    testImplementation project(':Plugins:PluginApi')
    testImplementation "junit:junit:4.12"
}

generateGrammarSource {
    arguments += ['-package', 'us.magicalash.weasel.plugin.docparser.generated', "-visitor"]
    outputDirectory = new File(buildDir.toString() + "/generated-src/antlr/main/us/magicalash/weasel/plugin/docparser/generated")
}

// copy all of the dependencies into libs as well
task copyToLib(type: Copy) {
    into "$buildDir/libs"
    from (configurations.runtimeClasspath - project(":CommonConfiguration").configurations.runtimeClasspath)
}

build.dependsOn(copyToLib)